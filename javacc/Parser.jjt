options {
  LOOKAHEAD = 1;
}


PARSER_BEGIN(Parser)

public class Parser {

    static int errors = 0;

}

PARSER_END(Parser)


//characters to ignore/skip
SKIP: {
  	  " " 
  	| "\r"	//move the cursor at start of current line 
  	| "\t"	//tab
  	| "\n"	//newline
  	| < "/*" (~["*"])* "*" (~["*","/"] (~["*"])* "*" | "*")* "/" >  /*multiline comment*/
  	| < "//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")> //inline comment
}

TOKEN: {
    < INTEGER : (["0" - "9"])+ >
    | < STATIC: "static" >
    | < INT: "int" >
    | < BOOLEAN: "boolean" >
    | < STRING: "string" >
    | < SEMICOLON: ";" >
    | < COMMA: "," >
    | < DOT: "." >
    | < TRUE: "true" >
    | < FALSE: "false" >
    | < NEW: "new" >
    | < THIS: "this" >
    | < LBRACKET: "{" >
    | < RBRACKET: "}" >
    | < LSQUAREBRACKET: "[" >
    | < RSQUAREBRACKET: "]" >
    | < LPAR: "(" >
    | < RPAR: ")" >
    | < PUBLIC: "public" >
    | < VOID: "void" >
    | < MAIN: "main" >
    | < RETURN: "return" >
    | < EXTENDS: "extends" >
    | < IMPORT: "import" >
    | < CLASS: "class" >
    | < WHILE: "while" >
    | < IF: "if" >
    | < ELSE: "else" >
    | < EQUAL: "=" >
    | < ADD: "+" >
    | < SUB: "-" >
    | < MUL: "*" >
    | < DIV: "/" >
    | < NOT: "!" >
    | < LESS: "<" >
    | < AND: "&&" >
    | < LENGTH: "length" >
    | < STRINGARRAY: "String[]" >
    | < IDENTIFIER: ["$","a"-"z", "A"-"Z", "_"](["$", "_", "a"-"z", "A"-"Z", "0"-"9"])* >
}


//DECLARATIONS

SimpleNode Program() : { Parser.errors = 0; }
{
    Imports() ClassDeclaration() <EOF> { return jjtThis; }
}

void Imports() #void: {}
{
  	(<IMPORT> ( StaticImport() | NonStaticImport() ) <SEMICOLON>)*
}

void StaticImport(): {}
{
    <STATIC> TerminalId() MethodImport()
}

void NonStaticImport() : {}
{
  	TerminalId() [<LPAR> ImportParamList() <RPAR> | MethodImport()]
}

void MethodImport() #Method: { Token t; }
{
  	<DOT> t = <IDENTIFIER> {jjtThis.value = t.image;} [<LPAR> ImportParamList() <RPAR>] [Return()]
}

void ImportParamList() #ParamList: {}
{
    [(Type() (<COMMA> Type())*) | <VOID>]
}

void Return() : {}
{
    (Type() | <VOID>)
}

void ClassDeclaration() #Class : {Token t;}
{
  	<CLASS> t = <IDENTIFIER> {jjtThis.value = t.image;} (<EXTENDS> <IDENTIFIER>)?
  	<LBRACKET>
  	  	(VarDeclaration() <SEMICOLON>)*
  	  	(MethodDeclaration())*
  	<RBRACKET>
}

void VarDeclaration() : {}
{
  	Type() TerminalId()
}


void MethodDeclaration() #Method : {Token t;}
{ 
  	<PUBLIC>
        (
            Type() t = <IDENTIFIER> {jjtThis.value = t.image;} <LPAR> MethodParamList() <RPAR> <LBRACKET>
            (
                (LOOKAHEAD(2) VarDeclaration() <SEMICOLON>)*
                (Statement())*
                MethodReturn()
            ) #Body
        |
            <STATIC> <VOID> t = <MAIN> {jjtThis.value = t.image;} <LPAR> MainParamList() <RPAR> <LBRACKET>
            (
                (LOOKAHEAD(2) VarDeclaration() <SEMICOLON>)*
                (Statement())*
            ) #Body
        )
  	<RBRACKET>
}

void MethodParamList() #ParamList: {}
{
    ( VarDeclaration() ( <COMMA> VarDeclaration() )* )?
}

void MethodReturn() #Return: {}
{
    <RETURN> Expression() <SEMICOLON>
}

void MainParamList() #ParamList: {}
{
    (<STRINGARRAY> <IDENTIFIER>)?
}
 
void Type() : {Token t;}
{
    t = <INT> {jjtThis.value = t.image;} (<LSQUAREBRACKET> <RSQUAREBRACKET> {jjtThis.value = "array";})?
    | t = <BOOLEAN> {jjtThis.value = t.image;}
    | t = <IDENTIFIER> {jjtThis.value = t.image;}
}

void WhileCondition() #Condition: {}
{
    try
    {
        <LPAR> Expression() <RPAR>
    }
    catch (ParseException e)
    {
        Parser.errors += 1;
        String expectedTokens = new String(" ");
        for (int i = 0; i < e.expectedTokenSequences.length; i++) {
            for (int j = 0; j < e.expectedTokenSequences[i].length; j++) {
                if (i != 0) 
                    expectedTokens += ", ";
                expectedTokens += e.tokenImage[e.expectedTokenSequences[i][j]];
            }
        }

        System.out.println(
            "Error in line " + e.currentToken.next.beginLine + 
            ", column " + e.currentToken.next.beginColumn +
            ": found \"" + e.currentToken.next + "\"" +
            " but was expecting" + expectedTokens + ".");

        if (Parser.errors >= 10) {
            throw new ParseException("Encountered too many parsing errors. Exiting now.");
        }
        Token t = getToken(0);
        while (true) {
            if (t.kind == RPAR && getToken(1).kind != RPAR) {
                break;
            }
            else if (getToken(1).kind == LBRACKET){
                break;
            }    

            t = getNextToken();
        }
    }
}

void Statement() #void: {}
{
    WhileLoop()
    | IfStatement()
    | Scope()
    | TerminalId() IdStatement() <SEMICOLON>
    | NonIdentifierExpression() <SEMICOLON>
}

void IdStatement() #void : {} {
    Assignment() 
    | ArrayExpression() (Assignment() | NonTerminalExpression0())
    | NonArrayExpression0()
}

void Assignment() #Assign(2) : {}
{
    <EQUAL> Expression()
}

void WhileLoop() #While : {}
{
    <WHILE> WhileCondition() Statement()
}

void IfStatement() : {}
{
    <IF> <LPAR> Expression() #Condition <RPAR> Statement() <ELSE> Statement()
}

void Scope(): {}
{
    <LBRACKET> (Statement())* <RBRACKET>
}

void DotExpression() #void: {}
{
    <DOT> (<LENGTH> #Length(1) | MethodInvocation())
}

void ArrayExpression() #Array(2): {}
{
    <LSQUAREBRACKET> Expression() <RSQUAREBRACKET> 
}

void MethodInvocation() #MethodInvocation(3) : {}
{
    TerminalId() <LPAR> ArgList() <RPAR> 
}

void ArgList(): {}
{
    (Expression() (<COMMA> Expression())*)?
}


/* -----===== Level 0 Expressions =====----- */

void Expression() #void : {}
{
    Expression1() AndExpression()
}

void NonIdentifierExpression() #void : {}
{
    NonIdentifierExpression1() AndExpression()
}

void NonArrayExpression0() #void : {}
{
    NonArrayExpression1() AndExpression()
}

void NonTerminalExpression0() #void : {}
{
    NonTerminalExpression1() AndExpression()
}

void AndExpression() #void : {} {
    <AND> Expression1() #And(2) AndExpression()
    | {}
}

/* -----===== Level 1 Expressions =====----- */

void Expression1() #void : {}
{
    Expression2() LessExpression()
}

void NonIdentifierExpression1() #void : {}
{
    NonIdentifierExpression2() LessExpression()
}

void NonArrayExpression1() #void : {}
{
    NonArrayExpression2() LessExpression()
}

void NonTerminalExpression1() #void : {}
{
    NonTerminalExpression2() LessExpression()
}

void LessExpression() #void : {} {
    <LESS> Expression2() #Less(2) LessExpression()
    | {}
}

/* -----===== Level 2 Expressions =====----- */

void Expression2() #void : {}
{
    Expression3() SumExpression()
}

void NonIdentifierExpression2() #void : {}
{
    NonIdentifierExpression3() SumExpression()
}

void NonArrayExpression2() #void : {}
{
    NonArrayExpression3() SumExpression()
}

void NonTerminalExpression2() #void : {}
{
    NonTerminalExpression3() SumExpression()
}

void SumExpression() #void : {} {
    <ADD> Expression3() #Add(2) SumExpression()
    | <SUB> Expression3() #Sub(2) SumExpression()
    | {}
}

/* -----===== Level 3 Expressions =====----- */

void Expression3() #void : {}
{
    Expression4() MulExpression()
}

void NonIdentifierExpression3() #void : {}
{
    NonIdentifierExpression4() MulExpression()
}

void NonArrayExpression3() #void : {}
{
    NonArrayExpression4() MulExpression()
}

void NonTerminalExpression3() #void : {}
{
    NonTerminalExpression4() MulExpression()
}

void MulExpression() #void : {} {
    <MUL> Expression4() #Mul(2) MulExpression()
    | <DIV> Expression4() #Div(2) MulExpression()
    | {}
}

/* -----===== Level 4 Expressions =====----- */

void Expression4() #void : {}
{
    (TerminalId() | TerminalNonId()) NonTerminalExpression4()
}

void NonIdentifierExpression4() #void : {}
{
    (TerminalNonId() | {}) NonTerminalExpression4()
}

void NonArrayExpression4() #void : {}
{
    DotExpression() NonTerminalExpression4()
    | {}
}

void NonTerminalExpression4() #void : {}
{
    ArrayExpression() NonTerminalExpression4()
    | NonArrayExpression4()
}

/* -----===== Terminal Expressions =====----- */

void TerminalNonId() #void: {Token t;}
{
    t = <INTEGER> {jjtThis.value = Integer.valueOf(t.image);} #Integer
    | t = <TRUE> {jjtThis.value = Boolean.valueOf(t.image);} #Boolean
    | t = <FALSE> {jjtThis.value = Boolean.valueOf(t.image);} #Boolean
    | <THIS> #This
    | <NEW> (
        <INT> <LSQUAREBRACKET> Expression() <RSQUAREBRACKET> #NewIntArray(1)
        | TerminalId() <LPAR> <RPAR> #NewObject(1)
    )
    | <NOT> Expression4() #Not(1)
    | <LPAR> Expression() <RPAR>
}

void TerminalId() #Identifier: {Token t;}
{
    t = <IDENTIFIER> {jjtThis.value = t.image;}
}